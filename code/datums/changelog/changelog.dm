/datum/changelog
	var/static/list/changelog_items = list()

/datum/changelog/ui_state()
	return GLOB.always_state

/datum/changelog/ui_interact(mob/user, datum/tgui/ui)
	ui = SStgui.try_update_ui(user, src, ui)
	if (!ui)
		ui = new(user, src, "Changelog")
		ui.set_autoupdate(FALSE)
		ui.open()

/datum/changelog/ui_act(action, list/params, datum/tgui/ui, datum/ui_state/state)
	. = ..()
	if(.)
		return
	if(action == "get_month")
		return ui.send_asset(get_changelog_asset(params["date"]))

/datum/changelog/ui_assets(mob/user)
	return list(get_changelog_asset(time2text(world.realtime, "YYYY-MM")))

/datum/changelog/ui_static_data()
	var/list/data = list( "dates" = list() )
	var/regex/ymlRegex = regex(@"\.yml", "g")

	for(var/archive_file in sort_list(flist("html/changelogs/archive/")))
		var/archive_date = ymlRegex.Replace(archive_file, "")
		data["dates"] = list(archive_date) + data["dates"]

	return data

/datum/changelog/proc/get_changelog_asset(date) as /datum/asset/changelog_item
	return changelog_items[date] ||= new /datum/asset/changelog_item(date)
