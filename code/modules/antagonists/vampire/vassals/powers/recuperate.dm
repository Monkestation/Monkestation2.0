/// Used by vassals
/datum/action/cooldown/vampire/recuperate
	name = "Sanguine Recuperation"
	desc = "Slowly heals you overtime using your master's blood, in exchange for some of your own blood and effort."
	button_icon_state = "power_recup"
	power_explanation = "Activating this Power will begin to heal your wounds.\n\
		You will heal Brute and Toxin damage at the cost of your Stamina and blood.\n\
		If you aren't a bloodless race, you will additionally heal Burn damage."
	vampire_power_flags = BP_AM_TOGGLE
	vampire_check_flags = BP_CANT_USE_WHILE_INCAPACITATED | BP_CANT_USE_WHILE_UNCONSCIOUS
	special_flags = NONE
	vitaecost = 1.5
	cooldown_time = 10 SECONDS

/datum/action/cooldown/vampire/recuperate/can_use()
	. = ..()
	if(!.)
		return FALSE

	if(owner.stat >= DEAD || owner.incapacitated(IGNORE_RESTRAINTS))
		owner.balloon_alert(owner, "you are incapacitated...")
		return FALSE

	var/mob/living/living_owner = owner
	if(!HAS_TRAIT(owner, TRAIT_NOBLOOD) && living_owner.blood_volume <= BLOOD_VOLUME_OKAY)
		owner.balloon_alert(owner, "not enough blood!")
		return FALSE

/datum/action/cooldown/vampire/recuperate/activate_power()
	. = ..()
	to_chat(owner, span_notice("Your muscles clench as your master's immortal blood mixes with your own, knitting your wounds."))
	owner.balloon_alert(owner, "recuperate turned on.")

/datum/action/cooldown/vampire/recuperate/UsePower()
	. = ..()
	if(!. || !currently_active)
		return

	var/mob/living/carbon/carbon_owner = owner
	if(!istype(carbon_owner))
		return

	var/needs_update = FALSE
	carbon_owner.set_jitter_if_lower(10 SECONDS)
	carbon_owner.stamina?.adjust_to(-vitaecost * 1.1, 5) // can't stamcrit you. barely.
	needs_update += carbon_owner.adjustBruteLoss(-2.5, updating_health = FALSE)
	needs_update += carbon_owner.adjustToxLoss(-2, updating_health = FALSE, forced = TRUE)
	// Plasmamen won't lose blood, they don't have any, so they don't heal from Burn.
	if(!HAS_TRAIT(carbon_owner, TRAIT_NOBLOOD))
		carbon_owner.blood_volume -= vitaecost
		needs_update += carbon_owner.adjustFireLoss(-1.5, updating_health = FALSE)

	// Stop Bleeding
	var/datum/wound/bloodiest_wound
	for(var/datum/wound/iter_wound as anything in carbon_owner.all_wounds)
		if(iter_wound.blood_flow && (!bloodiest_wound || (iter_wound.blood_flow > bloodiest_wound.blood_flow)))
			bloodiest_wound = iter_wound

	bloodiest_wound?.adjust_blood_flow(-0.25)

	for(var/obj/item/bodypart/bodypart as anything in carbon_owner.bodyparts)
		if(bodypart.generic_bleedstacks)
			bodypart.adjustBleedStacks(-1, 0)

	if(needs_update)
		carbon_owner.updatehealth()

/datum/action/cooldown/vampire/recuperate/continue_active()
	if(owner.stat == DEAD)
		return FALSE
	if(owner.incapacitated(IGNORE_RESTRAINTS))
		owner.balloon_alert(owner, "too exhausted...")
		return FALSE
	return TRUE

/datum/action/cooldown/vampire/recuperate/deactivate_power()
	owner.balloon_alert(owner, "recuperate turned off.")
	return ..()
